#!/usr/bin/awk -f
# cbrunt.tex.gen gnucobol/doc
#
# Copyright (C) 2015-2020 Free Software Foundation, Inc.
# Written by James K. Lowden, Simon Sobisch
#
# This file is part of GnuCOBOL.
#
# The GnuCOBOL compiler is free software: you can redistribute it
# and/or modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation, either version 3 of the
# License, or (at your option) any later version.
#
# GnuCOBOL is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with GnuCOBOL.  If not, see <https://www.gnu.org/licenses/>.

/^##$/  {
    print "@verbatim"
    section++
    next
}

!section && /^## /,/^##$/ {
    if( debug ) { print "input: ", NR, $0 }
    section = 1

    gsub(/\r/, "")

    if( /^##$/ ) { print "@c end of section 1"; next }

    sub(/^./, "")

    if( debug ) { print "inpu1: ", NR, $0 }

    if( sub(/^#/, "@section") ) {
	$0 = $0 "\n"
    }
    sub(/^ /, "")
    gsub(/[{}]/, "@&")

    # Wrap 1-line example in "code{...}"
    # s/\(Example:\)  \(.*\)$/\n\1 @code{\2}/g
    if( /Example:  / ) {
	needle = "Example:"
	n = index($0, needle "  ")
	line = sprintf( "%s\n%s @code{%s}",
			substr($0, 1, n), # prefix
			needle, 
			substr($0, n+1, length($0)) ) # remainder
	$0 = line
    }

    # Wrap example functions in "code{...}"
    # s/  \([^ ][^(]*\)  \([,.]\)/ @code{\1}\2/g
    # s/  \([^ ][^(]*\)  / @code{\1} /g
    # s/  \([^ ][^(]*\)$/ @code{\1}/g

    sub(/^$/, "@*")

    if( debug ) { printf "output: " }
    print
    next
}

#
# Other sections
#

section > 1 {
    sub(/^./, "")
    gsub(/\r/, "")

    # $SED -e 's/# \?TO-\?DO.*$//g'  
    gsub(/# \?TO-\?DO.*/, "")
    
    # -e 's/^#\( .*\)/@end verbatim\n@section\1\n@verbatim/g'
    if(  match($0, /^# /) ) {
	$0 = sprintf( "@end verbatim\n@section%s\n@verbatim",
		      substr($0, RLENGTH, length($0)) )
    }
    
    # -e 's/^ //g'
    sub(/^ /, "")

    print
}

END {
    print "@end verbatim"
}

